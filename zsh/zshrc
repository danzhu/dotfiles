# --- basic settings ---
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory histignorealldups sharehistory
setopt extendedglob
setopt autocd autopushd pushdsilent
bindkey -e


# --- completion ---
autoload -Uz compinit
compinit

zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' rehash true
zstyle ':completion:*:cd:*' ignore-parents parent pwd
zstyle ':completion::*:(rm|vim):*' ignore-line true

autoload -Uz add-zsh-hook

autoload -Uz chpwd_recent_dirs cdr
add-zsh-hook chpwd chpwd_recent_dirs

autoload -Uz run-help
unalias run-help 2> /dev/null
alias help='run-help'


# --- key bindings ---
function rationalise-dot() {
    if [[ $LBUFFER = *.. ]]; then
        LBUFFER+=/..
    else
        LBUFFER+=.
    fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

bindkey ' ' magic-space

WORDCHARS=''


# --- shell prompt ---
function prompt() {
    print ''

    print -nP '%B%K'
    if [[ $UID -eq 0 ]]; then
        # root
        print -nP ' %1F%m%f'
    elif [[ -n "$SSH_CONNECTION" ]]; then
        # ssh
        print -nP ' %m'
    fi

    # path
    print -nP ' %k %4F%~%f%b'

    # git prompt
    local gs
    if gs="$(git status --porcelain=v2 --branch 2> /dev/null)"; then
        local branch="$(prompt-grab "$gs" '# branch\.head (.*)')"

        if [[ "$branch" = '(detached)' ]]; then
            # detached head
            branch="$(git rev-parse --short HEAD)"
        fi

        # branch
        print -nP " %7F$branch%f"

        # ahead
        prompt-diverge "$gs" '# branch\.ab \+(.*) .*' '%6F' 'A%f'
        # behind
        prompt-diverge "$gs" '# branch\.ab .* -(.*)' '%5F' 'B%f'

        # unmerged
        prompt-count "$gs" '^u' '%1F' '!%f'
        # added
        prompt-count "$gs" '^[12] [MADRCU].' '%2F' '+%f'
        # changed
        prompt-count "$gs" '^[12] .[MADRCU]' '%3F' '~%f'
        # untracked
        prompt-count "$gs" '^\?' '%4F' '?%f'
    fi

    print ''
}
add-zsh-hook precmd prompt

function prompt-grab() {
    echo "$1" | sed -nE "s/^$2$/\\1/p"
}

function prompt-diverge() {
    local count=$(prompt-grab "$1" "$2")
    if [[ $count -ne 0 ]]; then
        print -nP " $3$count$4"
    fi
}

function prompt-count() {
    local lines
    if lines="$(echo "$1" | grep -E "$2")"; then
        print -nP " $3$(echo "$lines" | wc -l)$4"
    fi
}

PROMPT='%K%(?. .%1F!%f)%k '


# --- syntax highlighting ---
HL=/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
if [[ -f "$HL" ]]; then
    ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
    source "$HL"
fi
unset HL


# --- zsh-specific aliases ---
alias -s cc=vim
alias -s h=vim

alias -g G='| grep'
alias -g L='| less'


# --- common ---
source ~/.dotfiles/common/aliases
source ~/.dotfiles/common/functions
