# --- basic settings ---
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory histignorealldups sharehistory
setopt extendedglob
setopt autocd autopushd pushdsilent
bindkey -e


# --- completion ---
autoload -Uz compinit
compinit

zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' rehash true
zstyle ':completion:*:cd:*' ignore-parents parent pwd
zstyle ':completion::*:(rm|vim):*' ignore-line true

autoload -Uz add-zsh-hook

autoload -Uz chpwd_recent_dirs cdr
add-zsh-hook chpwd chpwd_recent_dirs

autoload -Uz run-help
unalias run-help
alias help='run-help'


# --- key bindings ---
function rationalise-dot() {
    if [[ $LBUFFER = *.. ]]; then
        LBUFFER+=/..
    else
        LBUFFER+=.
    fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

bindkey ' ' magic-space

WORDCHARS=''


# --- shell prompt ---
function prompt() {
    print ''
    print -nP '%B%K'
    if [[ $UID -eq 0 ]]; then
        print -nP ' %1F%n%f@%m'
    elif [[ -n "$SSH_CONNECTION" ]]; then
        print -nP ' %m'
    fi
    print -nP ' %k %4F%~%f%b'

    local branch
    if branch=$(git rev-parse --abbrev-ref HEAD 2> /dev/null); then
        local gitstatus=$(git status --porcelain 2> /dev/null)
        if echo "$gitstatus" | grep -qE '^[MADRCU].'; then
            print -nP '%2F'
        elif echo "$gitstatus" | grep -qE '^.[MADRCU]'; then
            print -nP '%1F'
        elif echo "$gitstatus" | grep -qE '^\?\?'; then
            print -nP '%3F'
        else
            print -nP '%7F'
        fi
        print -nP " $branch%f"
    fi

    print ''
}
add-zsh-hook precmd prompt

PROMPT='%K%(?. .%1F!%f)%k '


# --- syntax highlighting ---
HL=/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
if [[ -f "$HL" ]]; then
    ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
    source "$HL"
fi
unset HL


# --- zsh-specific aliases ---
alias -s cc=vim
alias -s h=vim

alias -g G='| grep'
alias -g L='| less'


# --- common ---
source ~/.dotfiles/common/aliases
source ~/.dotfiles/common/functions
