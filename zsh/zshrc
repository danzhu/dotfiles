# --- basic settings ---
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory extendedglob histignorealldups sharehistory
setopt autocd
bindkey -e


# --- completion ---
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' rehash true
zstyle ':completion:*:cd:*' ignore-parents parent pwd

autoload -Uz compinit
compinit

autoload -Uz add-zsh-hook
add-zsh-hook precmd prompt

autoload -Uz run-help
unalias run-help


# --- key bindings ---
function rationalise-dot() {
    if [[ $LBUFFER = *.. ]]; then
        LBUFFER+=/..
    else
        LBUFFER+=.
    fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

WORDCHARS=''


# --- shell prompt ---
function prompt() {
    local branch gitstatus
    print ''
    print -nP '%B%K'
    if [[ $UID -eq 0 ]]; then
        print -nP ' %1F%n%f@%m'
    elif [[ -n "$SSH_CONNECTION" ]]; then
        print -nP ' %m'
    fi
    print -nP ' %k %4F%~%f%b'
    if branch=$(git rev-parse --abbrev-ref HEAD 2> /dev/null); then
        gitstatus=$(git status --porcelain 2> /dev/null)
        if echo "$gitstatus" | grep -qE '^[MADRCU].'; then
            print -nP '%2F'
        elif echo "$gitstatus" | grep -qE '^.[MADRCU]'; then
            print -nP '%1F'
        elif echo "$gitstatus" | grep -qE '^\?\?'; then
            print -nP '%3F'
        else
            print -nP '%7F'
        fi
        print -nP " $branch%f"
    fi
    print ''
}

PROMPT='%K%(?. .%1F!%f)%k '


# --- syntax highlighting ---
HL=/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
if [[ -f "$HL" ]]; then
    ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
    source "$HL"
fi
unset HL


# --- zsh-specific aliases ---
alias -s cc=vim
alias -s h=vim


# --- common ---
source ~/.dotfiles/common/aliases
source ~/.dotfiles/common/functions
