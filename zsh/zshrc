# --- basic settings ---
HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt appendhistory histignorealldups sharehistory
setopt extendedglob
setopt autocd autopushd pushdsilent
bindkey -e


# --- completion ---
autoload -Uz compinit
compinit

zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' glob 1
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]} r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' rehash true
zstyle ':completion:*:cd:*' ignore-parents parent pwd
zstyle ':completion::*:(rm|vim):*' ignore-line true

autoload -Uz add-zsh-hook

autoload -Uz chpwd_recent_dirs cdr
add-zsh-hook chpwd chpwd_recent_dirs

autoload -Uz run-help
unalias run-help 2> /dev/null
alias help='run-help'


# --- key bindings ---
function rationalise-dot() {
    if [[ $LBUFFER = *.. ]]; then
        LBUFFER+=/..
    else
        LBUFFER+=.
    fi
}
zle -N rationalise-dot
bindkey . rationalise-dot

bindkey ' ' magic-space

WORDCHARS=''


# --- shell prompt ---
function prompt() {
    print ''
    print -nP '%B%K'
    if [[ $UID -eq 0 ]]; then
        print -nP ' %1F%n%f@%m'
    elif [[ -n "$SSH_CONNECTION" ]]; then
        print -nP ' %m'
    fi
    print -nP ' %k %4F%~%f%b'

    local gs
    if gs="$(git status --porcelain=v2 --branch 2> /dev/null)"; then
        local lines
        if lines="$(echo "$gs" | grep -E '^u')"; then
            # unmerged
            color='%5F'
        elif lines="$(echo "$gs" | grep -E '^[12] [MADRCU].')"; then
            # added
            color='%2F'
        elif lines="$(echo "$gs" | grep -E '^[12] .[MADRCU]')"; then
            # changed
            color='%1F'
        elif lines="$(echo "$gs" | grep -E '^\?')"; then
            # untracked
            color='%3F'
        else
            # normal
            color='%7F'
        fi

        local count="$(echo "$lines" | wc -l)"
        if [[ "$lines" != "" && $count > 0 ]]; then
            count=" $count"
        else
            count=''
        fi

        local branch="$(echo "$gs" | sed -nE 's/^# branch\.head (.*)$/\1/p')"

        local ahead="$(echo "$gs" | sed -nE 's/^# branch.ab( \+.*) .*$/\1/p')"
        if [[ "$ahead" = ' +0' ]]; then
            ahead=''
        fi

        local behind="$(echo "$gs" | sed -nE 's/^# branch.ab .*( -.*)$/\1/p')"
        if [[ "$behind" = ' -0' ]]; then
            behind=''
        fi

        print -nP " $color$branch$count%f$ahead$behind"
    fi

    print ''
}
add-zsh-hook precmd prompt

PROMPT='%K%(?. .%1F!%f)%k '


# --- syntax highlighting ---
HL=/usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
if [[ -f "$HL" ]]; then
    ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
    source "$HL"
fi
unset HL


# --- zsh-specific aliases ---
alias -s cc=vim
alias -s h=vim

alias -g G='| grep'
alias -g L='| less'


# --- common ---
source ~/.dotfiles/common/aliases
source ~/.dotfiles/common/functions
